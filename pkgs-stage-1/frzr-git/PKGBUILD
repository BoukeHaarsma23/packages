# Maintainer: Alesh Slovak <aleshslovak@gmail.com>

pkgname=frzr-git
pkgver=r250.bdfbb1f
pkgrel=1
pkgdesc="A deployment and update system for read-only btrfs subvolume based operating systems"
arch=('any')
url="https://github.com/boukehaarsma23/frzr"
license=('MIT')
depends=('btrfs-progs' 'parted' 'libnewt' 'dosfstools' 'jq' 'util-linux' 'skopeo')

source=(frzr::'git+https://github.com/boukehaarsma23/frzr.git'
        '99-frzr-kernel-install.hook')
md5sums=('SKIP'
         '6bc04b77f135f2bf7d07c2f8c0ffaca0')
sha256sums=('SKIP'
            '0dc3772b4c9a96ad33993be1245f23af32ee765ad6fca4e2310a8683ada5cbcc')

pkgver() {
  cd "${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}


package() {
    cd "${pkgname%-git}"
    mkdir -p "$pkgdir/usr/lib/frzr"
    mkdir -p "$pkgdir/usr/lib/systemd/system"
    mkdir -p "$pkgdir/usr/share/libalpm/hooks"
    PREFIX="$pkgdir" make install
    #install -m 644 "$srcdir/frzr-$pkgver/mkinitcpio.conf" "$pkgdir/usr/lib/frzr"
    install -m 644 "$srcdir/frzr/frzr-autoupdate.service" "$pkgdir/usr/lib/systemd/system"
    install -m 644 "$srcdir/frzr/frzr-autoupdate.timer" "$pkgdir/usr/lib/systemd/system"
    install -m 644 "$srcdir/99-frzr-kernel-install.hook" "$pkgdir/usr/share/libalpm/hooks"

    echo 'g frzr 379 - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}
